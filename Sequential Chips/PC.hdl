// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=fromreg, out=added);

    Mux8Way16(sel[2]=reset, sel[1]=load, sel[0]=inc, 
	    a=fromreg, // 000
	    b=added, // 001
	    c=in, // 010
	    d=in, // 011
	    e=false, // 100
	    f=false, // 101
	    g=false, // 110
	    h=false, // 111
	    out=sink
	);

    Register(in=sink, load=true, out=fromreg, out=out);
}
